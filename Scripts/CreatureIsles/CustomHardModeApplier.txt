



begin script CustomHardModePlayerInfluence
	Town0 = get town with id 0
	Town1 = get town with id 1
	Town2 = get town with id 2
	Town3 = get town with id 3
	Town4 = get town with id 4
	Town5 = get town with id 5
	Town6 = get town with id 6
	Town7 = get town with id 7
	Town8 = get town with id 8
	Town9 = get town with id 9
	Town10 = get town with id 10
	Town11 = get town with id 11
	Town12 = get town with id 12
	Town13 = get town with id 13
	Town14 = get town with id 14
	Town15 = get town with id 15
	
	
start
	// Land
	while Difficulty == 1
	
		//Reduce influence to only 33% of normal for Player 1. Only used in Hard Mode!
		if get PLAYER of Town0 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(0, 0.33333333)"
		end if
		
		if get PLAYER of Town1 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(1, 0.33333333)"
		end if

		if get PLAYER of Town2 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(2, 0.33333333)"
		end if

		if get PLAYER of Town3 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(3, 0.33333333)"
		end if
		
		if get PLAYER of Town4 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(4, 0.33333333)"
		end if
		
		if get PLAYER of Town5 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(5, 0.33333333)"
		end if
		
		if get PLAYER of Town6 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(6, 0.33333333)"
		end if
		
		if get PLAYER of Town7 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(7, 0.33333333)"
		end if
		
		if get PLAYER of Town8 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(8, 0.33333333)"
		end if
		
		if get PLAYER of Town9 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(9, 0.33333333)"
		end if
		
		if get PLAYER of Town10 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(10, 0.33333333)"
		end if
		
		if get PLAYER of Town11 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(11, 0.33333333)"
		end if
		
		if get PLAYER of Town12 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(12, 0.33333333)"
		end if
		
		if get PLAYER of Town13 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(13, 0.33333333)"
		end if
		
		if get PLAYER of Town14 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(14, 0.33333333)"
		end if
		
		if get PLAYER of Town15 == 1
			run map script line "SET_A_TOWNS_INFLUENCE_MULTIPLIER(15, 0.33333333)"
		end if
		
		wait 3 seconds
	end while
end script CustomHardModePlayerInfluence


//Make gods attack other creatures when in their influence. 
begin script CustomHardMode_GodAttack(PlayerNum)
	LoopForever = 1

	MyInfluence = 0 //Get influence of PlayerNum
	OtherInfluence = 0 //Get influence of other player(s).
	
	MyLightning = 0
	MyFreeze = 0
	
	CheckPlayer = 1


start
	wait 30 seconds // Wait 30 seconds before starting.
	
	while LoopForever == 1
		
		//Check for 4 other players. (Maybe upgrade this to 7?)
		while CheckPlayer <= 7
		
			//Skip this if checking self. In other words, don't attack your own creature in own influence!
			if PlayerNum != CheckPlayer 
			
				//Check if other player is an ally. Don't attack ally, duh.
				if get player PlayerNum ally percentage with player CheckPlayer < 1.0
					
					MyInfluence = get player PlayerNum raw influence at [get player CheckPlayer creature] //Check own player influence with enemy creature.
					OtherInfluence = get player CheckPlayer raw influence at [get player CheckPlayer creature] //Check other player influence with enemy creature.
					
					//If other player's creature is in own influence.
					if MyInfluence > OtherInfluence
					
						//If the enemy creature is still alive.
						if HEALTH of get player CheckPlayer creature != 0

							move computer player PlayerNum to [get player CheckPlayer creature] + [0,25,0] speed 100000 // Move god to the enemy creature
							wait 1.0 seconds
							
							//Cast Freeze Miracle. (TODO: Add support for checking if player has this spell)
							MyFreeze = cast CREATURE_SPELL_FREEZE spell on get player CheckPlayer creature from computer player PlayerNum position + [0,25,0] radius 0 time 20 curl 0
							wait 3.00 seconds
							
							MyInfluence = get player PlayerNum raw influence at [get player CheckPlayer creature] //Check own player influence with enemy creature.
							OtherInfluence = get player CheckPlayer raw influence at [get player CheckPlayer creature] //Check other player influence with enemy creature.
						
							//If other player's creature is in own influence. 2nd check to ensure god will not cheat.
							if MyInfluence > OtherInfluence
							
								//Adjust player's position.
								move computer player PlayerNum to [get player CheckPlayer creature] + [number from 10 to 20, 25, number from 10 to 20] speed 10000 // Move the player to the enemy god's creature
								wait 1.0 seconds
								
								//Cast Lightning Bolt at enemy Creature. (TODO: Add support for checking if player has this spell)
								MyLightning = cast SPELL_LIGHTNING_LEVEL_1 spell at [get player CheckPlayer creature] from computer player PlayerNum position + [0,25,0] radius 5.0 time 5 curl 0
								wait 5.00 seconds
								
								release computer player PlayerNum
								wait 5 seconds
								
								MyInfluence = get player PlayerNum raw influence at [get player CheckPlayer creature] //Check own player influence with enemy creature.
								OtherInfluence = get player CheckPlayer raw influence at [get player CheckPlayer creature] //Check other player influence with enemy creature.
							end if
						end if
					end if
				end if
			end if
			
		wait 1 seconds //Small cooldown.
		CheckPlayer++ //Check the next player.
		set computer player PlayerNum personality "ReactToAggressiveCreature" 0	 //Just to be sure incase external script changes this...
		end while
		
		//Keep loop forever.
		if CheckPlayer >= 4
			CheckPlayer = 1
		end if
		
	end while


end script CustomHardMode_GodAttack




//Adjust AI stats and behaviours.
begin script CustomHardMode_ImproveAIStats(PlayerNum)

start
	wait 5 seconds

	set computer player PlayerNum speed 3000.0
	set computer player PlayerNum personality "TakeOverTownByImpressing" 1.00
	set computer player PlayerNum personality "DefeatPlayer" 1.00
	set computer player PlayerNum personality "ExpandInfluence" 1.00

end script CustomHardMode_ImproveAIStats




// Make AI creature capture towns.
begin script CustomHardMode_CreatureCaptureTown(PlayerNum, TownNum)
	Town = get town with id TownNum
	TownStore = get STORE in Town
	Creature = 0 //get player 3 creature
	TotalBelief = 0
start
	Creature = get player PlayerNum creature
	wait 0.3 seconds
	
	if get PLAYER of Town <= 0
		if size of Town > 0 //Check if town not dead. Should also cover if town doesn't exist.
		
			//Attach creature to that town's village store.
			attach Creature leash to TownStore
			
			//Wait until town is not neutral or town dies and if the leash object exists (village store). Also, periodically add some extra belief.
			while get PLAYER of Town <= 0 and size of Town > 0 and Creature leashed
				TotalBelief = get Town belief for player PlayerNum
				TotalBelief += 0.001
				set Town player PlayerNum belief TotalBelief //Will give about 1 belief per 2 seconds. For a 1k town, will take 33.3 mins to capture.
				wait 2 seconds
			end while
			
			//Release the Creature.
			detach Creature leash
			release Creature
			
		end if
	end if
end script CustomHardMode_CreatureCaptureTown



// Make creature attack another player (citadel).
begin script CustomHardMode_CreatureAttackPlayer(PlayerNum)
	Creature = 0
	Citadel = PlayerNum
	PlayerToAttack = PlayerNum
start
	Creature = get player PlayerNum creature
	wait 1.0 seconds
	
	//Select a player to attack. (That isn't ourself). TO-DO: Need to add Ally support here
	while PlayerToAttack == PlayerNum
		PlayerToAttack = number from 1 to 4
	end while
	
	//Get the temple.
	Citadel = get CITADEL at player PlayerToAttack temple position radius 100
	wait 1.0 seconds
	
	//Make creature only want to attack and attach to temple.
	set Creature only desire CREATURE_DESIRE_ANGER
	attach Creature leash to Citadel
	
	//Wait until unleashed, then disable anger.
	wait until not Creature leashed
	detach Creature leash
	set Creature disable only desire
	release Creature

end script CustomHardMode_CreatureAttackPlayer





//Make gods impress towns + attack players.
begin script CustomHardMode_ImproveAIController(PlayerNum)
	Target_Town = 11
	ComputerAIAction = 0

start

	wait 60 seconds // Wait 10 minutes before starting.
		
	while Difficulty == 1

		//Controls the actions of the god.
		//if InfluenceP3 <= InfluenceP1
		//	ComputerAIAction = 0 //number from 1 to 1
		//end if
		
		
		//If no action selected. Main controller of creature action selector.
		if ComputerAIAction == 0
		
			//90% chance to impress village.
			if number from 1 to 100 < 90
				Target_Town = number from 0 to 12
				ComputerAIAction = 1
				
			//10% chance to attack.
			else
				ComputerAIAction = 2
			end if
			
		end if
		
		// Impress Towns.
		if ComputerAIAction == 1
			run script CustomHardMode_CreatureCaptureTown(PlayerNum, Target_Town)
			ComputerAIAction = 0
		end if
		
		//Attack another player's temple.
		if ComputerAIAction == 2
			run script CustomHardMode_CreatureAttackPlayer(PlayerNum)
			ComputerAIAction = 0
		end if
		
		wait 1 second

	end while

end script CustomHardMode_ImproveAIController





begin script CustomHardModeApplier

start

	//Do a check for HardMode.
	if Difficulty == 1

		//Remove hand out influence.
		disable player 1 virtual influence
		
		//Land 2.
		if LandNumber == 2
		
			//Adjust the player's influence
			run background script CustomHardModePlayerInfluence
			
			//Let Khazar defend too. Sorta improve.
			run background script CustomHardMode_GodAttack(2)
			run background script CustomKillCreatureFix(2)
			
			//Improve AI (Lethys).
			run background script CustomHardMode_ImproveAIStats(3)
			run background script CustomHardMode_ImproveAIController(3)
			run background script CustomHardMode_GodAttack(3)
			run background script CustomKillCreatureFix(3)

		end if
		
		
		
		//Land 3.
		if LandNumber == 3
		
			//Adjust the player's influence
			run background script CustomHardModePlayerInfluence
			
			//Improve AI (Lethys).
			run background script CustomHardMode_ImproveAIStats(2)
			//run background script CustomHardMode_ImproveAIController(2)
			//run background script CustomHardMode_GodAttack(2)
			run background script CustomKillCreatureFix(2)

		end if
		
		
		
		//Land 4.
		if LandNumber == 4
		
			//Adjust the player's influence
			run background script CustomHardModePlayerInfluence

		end if
		
		
		
		//Land 5.
		if LandNumber == 5
		
			//Adjust the player's influence
			run background script CustomHardModePlayerInfluence
			
			//Improve AI (Nemesis).
			run background script CustomHardMode_ImproveAIStats(2)
			run background script CustomHardMode_ImproveAIController(2)
			run background script CustomHardMode_GodAttack(2)
			run background script CustomKillCreatureFix(2)

		end if
		
	end if
	

	
end script CustomHardModeApplier